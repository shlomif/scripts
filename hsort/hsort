#!/usr/bin/perl -w
#
# $Id$
#
# The author disclaims all copyrights and releases this script into the
# public domain.
#
# "Human" sort that should emulate behaviour of Finder (as of Mac OS X
# 10.2). Sorts numeric values so 10 appears after 2, and compares in case-
# insensitive manner. Note that filesystems cannot have duplicate
# filenames, so this script filters out repeated input lines.
#
# Pass a list of files to merge and sort, or supply the data to be
# sorted on standard input:
#
# hsort file1 file2 file3
# ls | hsort

my @input = <>;
keys my %lookup = @input;

@lookup{ map {
  my $new;
  ($new = $_) =~ s/(\d+)/unpack "B32", pack "N",$1/eg;
  lc $new
} @input } = @input;

print @lookup{sort keys %lookup};

=pod SCRIPT CATEGORIES

Utilities

=cut
