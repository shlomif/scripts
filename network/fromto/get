#!/usr/bin/perl
#
# Connects to a host/port and emits anything from such to stdout (run
# srvsend on the other end).

use strict;
use warnings;
use IO::Socket::INET;

die "Usage: $0 host port > outfile\n" if @ARGV != 2;
my $host = shift;
my $port = shift;

my $sock = IO::Socket::INET->new(
  PeerAddr => $host,
  PeerPort => $port,
  Proto    => 'tcp',
) or die "$0: could not connect to $host:port: $@\n";

binmode $sock;
binmode STDOUT;

my $buf;
while (1) {
  my $buflen = sysread( $sock, $buf, 1440 );
  die "sysread() failed: $!\n" if !defined $buflen;
  last if $buflen == 0;    # EOF
  syswrite STDOUT, $buf, $buflen;
}

exit 0;
