#!/usr/bin/env perl
#
# build a regular expression of things for nocolor(1) to nuke, mostly
# based on what I do not like. not complete, as rarely seen inputs are
# not filtered for. this would be better handled by a state machine
# instead of a regular expression on account of legal yet uncommon
# sequences such as
#
#   \033[32;32;32;32;32;32;32;32;32;32;32;32;32;32;32;32m

use 5.14.0;
use warnings;

use Regexp::Assemble;
my $ra = Regexp::Assemble->new;

# Regexp::Assemble is a bit screwy if you give it the CSI "\e[" sequence
# so instead that's here "EX" and will need to be changed later
#
# faint, italics, blink, invisible, crossed out
for my $i ( 2, 5, 7, 8, 9 ) { $ra->add("EX${i}m") }

# "double underline"
$ra->add("EX21m");

# ANSI colors (no "8" and "9" is default so only 0-7 for 2nd number)
for my $i ( 3, 4 ) {
    for my $j ( 0 .. 7 ) { $ra->add("EX${i}${j}m") }
}

# 16-bit colors
for my $i ( 9, 10 ) {
    for my $j ( 0 .. 7 ) { $ra->add("EX${i}${j}m") }
}

# ISO-8613-3 controls 
for my $i ( 3, 4 ) {
    # this is a bit broad, in theory if you use : instead of ; anything
    # subsequent is supposed to only be a : not a ; (iTerm.app does not
    # support the : form)
    $ra->add("EX${i}8[:;]2[:;][0-9]{1,3}[;:][0-9]{1,3}[;:][0-9]{1,3}m");

    # KLUGE the numeric range on this one for iTerm.app appears to be
    # INT_MAX so match up to that number of digits. if some terminal
    # reads 64 bits or more of data the range will need to go up (and
    # likewise NOCOLOR_FRAG_MAX)
    $ra->add("EX${i}8[:;]5[:;][0-9]{1,10}m");
}

# this is for the special \e[A;Bm form (longer runs ...;C;D;...m of this
# form are not matched, if so, NOCOLOR_FRAG_MAX will need to be
# increased) where A and B are one of the following
my @codes = ( 0 .. 9, 21 .. 49, 90 .. 97, 100 .. 107 );
for my $c1 (@codes) {
    for my $c2 (@codes) {
        $ra->add("EX${c1};${c2}m");
    }
}

# gussy up for PCRE and present as a C-string
my $s = $ra->re;

$s =~ s/\?\^\:/?:/;
$s =~ s/E/\\033/g;
$s =~ s/X/\\\\[/g;
$s =~ s/\\d/[0-9]/g;

print qq{"$s+"};
