#!/usr/bin/perl
#
# Compile and run random C snippets specified on command line:
#
# c-fu 'puts("Hello World")'
#
# Each argument (or line of standard input if the only argument is '-')
# is treated as a C statement (and a semi-colon appended if not
# present).
#
# Various compiler options are set to various defaults, or can be
# customized via the CC or CFLAGS environment variables. If in doubt,
# read the source.
#
# The directory used to hold temporary sources and the executable can be
# set by altering the TMPDIR environment variable:
#
# TMPDIR=/var/tmp c-fu ...

use strict;
use warnings;

use File::Basename qw/basename/;
use File::Spec ();
use File::Temp qw/tempfile/;
use Getopt::Long qw/GetOptions/;
use IO::Handle;

my $BASENAME = basename($0);
my $CC = exists $ENV{'CC'} ? $ENV{'CC'} : 'cc';
my @CFLAGS = split ' ', exists $ENV{'CFLAGS'} ? $ENV{'CFLAGS'} : '-O2 -Wall';

my $echo_code   = 0;
my $dry_run     = 0;
my $exit_status = 0;

my ( $code_fh, $code_fname, $bin_fname );
my ( @defines, $c_template );

########################################################################
#
# MAIN

unless (@ARGV) {
  warn "Usage: $BASENAME [c-statement-1 c-stmt-2 ..|-]\n";
  exit 64;
}

GetOptions(
  'show-code|l' => \$echo_code,
  'dry-run|n'   => \$dry_run,
  'defines|d=s' => \@defines
);

for my $define (@defines) {
  $define .= "\n" unless $define =~ m/\n$/;
}

if ( !grep { $_ eq '-x' } @CFLAGS ) {
  push @CFLAGS, qw/-x c/;
}

if ( @ARGV == 1 and $ARGV[0] eq '-' ) {
  chomp( @ARGV = <STDIN> );
}

for my $statement (@ARGV) {
  $statement .= ";\n" unless $statement =~ m/;\s*$/;
}

# PID only in filename as otherwise src and obj would only be related by
# perhaps the filesystem mtime
( $code_fh, $code_fname ) = tempfile(
  "$BASENAME.src-$$.XXXXXXXXXX",
  DIR    => File::Spec->tmpdir,
  UNLINK => 0
);
( undef, $bin_fname ) = tempfile(
  "$BASENAME.obj-$$.XXXXXXXXXX",
  DIR    => File::Spec->tmpdir,
  UNLINK => 0
);

$c_template = <<"END_TMPL";

#include <arpa/inet.h>
#include <sys/time.h>
#include <sys/types.h>
#include <sys/socket.h>

#ifdef __linux__
#include <linux/limits.h>
#else
#include <sys/syslimits.h>
#endif

#include <err.h>
#include <errno.h>
#include <ctype.h>
#include <libgen.h>
#include <limits.h>
#include <math.h>
#include <signal.h>
#include <stdio.h>
#include <stdlib.h>
#include <string.h>
#include <sysexits.h>
#include <time.h>
#include <unistd.h>

@defines
int main(int argc, const char *argv[]) {
@ARGV
  return 0;
}
END_TMPL

$code_fh->autoflush(1);
print $code_fh $c_template;
print $c_template if $echo_code;

if ( !$dry_run ) {
  my $s = system( $CC, @CFLAGS, $code_fname, '-o', $bin_fname );
  if ( $s != 0 ) {
    print $c_template;
    die "error: compile failed: $?\n";
  }
  system($bin_fname);
  $exit_status = $? >> 8;
}

unlink $code_fname, $bin_fname;

exit $exit_status;
