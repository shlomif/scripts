#!/usr/bin/env perl
#
# Wrap timidity, reading *.ly files for extra command line arguments.

use 5.14.0;
use warnings;

use File::Basename qw(dirname);
use File::stat qw(stat);
use List::UtilsBy qw(max_by);
use Text::ParseWords qw(shellwords);

my @TIMIDITY_CMD = qw/timidity -A90a --quiet=1/;

my $midi_file = shift // most_recent_midi();

my $ly_file = $midi_file =~ s/.[^.]+$/.ly/r;
if ( -f $ly_file ) {
    unshift @ARGV, parse_ly( $ly_file, dirname($midi_file) );
}

exec( @TIMIDITY_CMD, @ARGV, $midi_file )
  or die "could not exec '@TIMIDITY_CMD @ARGV $midi_file': $!\n";

sub most_recent_midi {
    max_by { stat($_)->mtime } glob '*.midi';
}

sub parse_ly {
    my ( $filename, $path ) = @_;
    my @argv_additions;

    open my $lyfh, '<', $filename or die "could not open '$filename': $!\n";
    while ( readline $lyfh ) {
        # which I disable when needed with a %% prefix...
        if (m/^%\s+timidity\s+([^%]+)/) {
            push @argv_additions, shellwords($1);
            last;
        }
    }
    for my $extra (@argv_additions) {
        $extra =~ s{file=(?![./])}{file=$path/};
    }
    return @argv_additions;
}
