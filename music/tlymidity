#!/usr/bin/env perl
#
# Wrap timidity, reading *.ly files for extra command line arguments.

use 5.14.0;
use warnings;

use File::Basename qw(dirname);
use File::stat qw(stat);
use List::UtilsBy qw(max_by);
use Text::ParseWords qw(shellwords);

my @TIMIDITY_CMD = qw/timidity -A90a --quiet=1/;

my $midi_file = shift // most_recent_midi();

my $ly_file = $midi_file =~ s/.[^.]+$/.ly/r;
if ( -f $ly_file ) {
    my ( $moreargs, $midiu ) = parse_ly( $ly_file, dirname($midi_file) );
    unshift @ARGV, @$moreargs if @$moreargs;
    if (@$midiu) {
        system( 'midiutil', @$midiu, $midi_file ) == 0
          or die "miditil @$midiu failed: $?\n";
    }
}

exec( @TIMIDITY_CMD, @ARGV, $midi_file )
  or die "could not exec '@TIMIDITY_CMD @ARGV $midi_file': $!\n";

sub most_recent_midi {
    max_by { stat($_)->mtime } glob '*.midi';
}

sub parse_ly {
    my ( $filename, $path ) = @_;
    my ( @argv_additions, @midiutil );

    open my $lyfh, '<', $filename or die "could not open '$filename': $!\n";
    while ( readline $lyfh ) {
        # lilypond file assumed to have a header block of leading
        # comments; save CPU by not looping past this
        last if m/^[^%]/;

        if ( !@argv_additions and m/^%\s+timidity\s+([^%\n\r]+)/ ) {
            push @argv_additions, shellwords($1);
        }
        if ( !@midiutil and m/^%\s+midiutil\s+(\w+)/aa ) {
            push @midiutil, $1;
        }
    }
    for my $extra (@argv_additions) {
        $extra =~ s{file=(?![./])}{file=$path/};
    }
    return \@argv_additions, \@midiutil;
}
