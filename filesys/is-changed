#!/usr/bin/env perl

use strict;
use warnings;
use Digest::SHA1;

my $duration = shift;
my $command  = shift;
die "Usage: is-changed time-spec command file [file2 ..]\n"
  if !defined $duration
  or !defined $command
  or !@ARGV;

my %factors = ( s => 1, m => 60, h => 3600, d => 86400 );
my $seconds;
while ( $duration =~ m/([0-9]+)([smhd])?/g ) {
    my $value = $1;
    $value *= $factors{$2} if $2;
    $seconds += $value;
}
die "Usage: is-changed time-spec command file [file2 ..]\n" if $seconds == 0;

my ( @changes, %prev );
while (1) {
    @changes = ();
    for my $file (@ARGV) {
        my ( $fh, $checksum );
        open $fh, '<', $file or next;
        eval { $checksum = Digest::SHA1->new->addfile($fh)->digest };
        next if $@;

        if ( exists $prev{$file} and $prev{$file} ne $checksum ) {
            push @changes, $file;
        }
        $prev{$file} = $checksum;
    }
    if (@changes) {
        # TWEAK or pass the changed files to the command, if need be
        system($command) == 0 or exit 1;
    }
    # TWEAK if the command takes awhile then that runtime might be
    # subtracted from this value
    sleep $seconds;
}
