#!/usr/bin/env perl
#
# Means to prompt for human interaction.

use 5.16.0;
use warnings;
use Getopt::Long qw(GetOptions);
use Term::ReadKey qw(ReadKey ReadMode);
use Time::HiRes qw(alarm);

open my $ttyfh, '+<', '/dev/tty' or die "getraw: could not open tty: $!\n";
# NOTE olden versions of perl may require IO::Handle loaded for this
$ttyfh->autoflush(1);

Getopt::Long::Configure("bundling");
GetOptions(
    'delay|d=f'     => \my $Flag_Delay,
    'help|h|?'      => \&emit_help,
    'newline|n'     => \my $Flag_Newline,
    'return|r'      => \my $Flag_Return,
    'sigstatus|S=i' => \my $Flag_Signal_Status,
    'spec|o=s'      => \my $Flag_Spec,
    'tostatus|T=i'  => \my $Flag_Timeout_Status,
    'timeout|t=f'   => \my $Flag_Timeout,
) or exit 65;

if ( defined $Flag_Delay and $Flag_Delay <= 0 ) {
    warn "getraw: delay must be a positive value\n";
    exit 65;
}
if ( defined $Flag_Timeout and $Flag_Timeout <= 0 ) {
    warn "getraw: timeout must be a positive value\n";
    exit 65;
}
$Flag_Signal_Status  //= 3;
$Flag_Timeout_Status //= 4;

my $keys2exit = parse_spec(
      defined $Flag_Spec       ? $Flag_Spec
    : exists $ENV{GETRAW_SPEC} ? $ENV{GETRAW_SPEC}
    :                            'Yy:0 Nn:2'
);

print $ttyfh "@ARGV" if @ARGV;

for my $s (qw/HUP INT TERM USR1 USR2/) { $SIG{$s} = \&sig_bail }
ReadMode( 3, $ttyfh );

eval {
    if ($Flag_Timeout) {
        $SIG{ALRM} = sub { die "alarm\n" };
        alarm $Flag_Timeout;
    }

    # eat up leading input within the timeout to avoid an infinite
    # wait in the (unlikely?) case there is infinite input being fed
    # in fast enough
    if ( defined $Flag_Delay ) {
        select( undef, undef, undef, $Flag_Delay );
        # eager beaver prevention: drain everything queued
        my $key;
        do {
            # NOTE PORTABILITY may die with fatal error (see docs for function)
            $key = ReadKey( -1, $ttyfh );
        } until !defined $key;
    }

    while (1) {
        my $key = ReadKey( 0, $ttyfh );
        my $code = $keys2exit->{$key};
        $code = $keys2exit->{any} if !defined $code;
        exit $code if defined $code;
    }
    alarm 0 if $Flag_Timeout;
};
if ($@) {
    if ( $@ eq "alarm\n" ) {
        exit $Flag_Timeout_Status;
    } else {
        die "getraw: unexpected failure: $@";
    }
}

END {
    if ( defined $ttyfh ) {
        print $ttyfh "\r" if $Flag_Return;
        print $ttyfh "\n" if $Flag_Newline;
        ReadMode( 0, $ttyfh );
        close $ttyfh;
    }
}

######################################################################
#
# SUBROUTINES

sub emit_help {
    warn <<"END_USAGE";
Usage: getraw [options] [prompt message]

Means to prompt for human interaction.

  -d ff  Delay readkey by this many fractional seconds.
  -n     Emit newline to tty before exit.
  -r     Emit return to tty before exit.
  -o ss  Use custom key to exit code specification.
  -t dur Timeout getraw after this many fractional seconds.

END_USAGE
    exit 64;
}

sub parse_spec {
    my ($string) = @_;
    my %keys2exit;

    for ( split /(?<!\\)[ ]+/, $string ) {
        my ( $keys, $status ) = /(.+?):([0-9]+)?$/;
        die "getraw: could not parse specification '$_'" if !defined $keys;

        if ( $keys =~ s/(?<!\\)\*// ) {
            # default action via the so-called "any" key
            $keys2exit{any} = $status;
        }

        # unescape backwhacked things (\r, etc.)
        $keys =~ s/(\\[aefnrt])/qq("$1")/eeg;

        for my $k ( split //, $keys ) {
            die "getraw: specification for key '$k' already exists\n"
              if exists $keys2exit{$k};
            $keys2exit{$k} = $status;
        }
    }

    die "getraw: unable to parse specification '$string'"
      if keys %keys2exit == 0;

    return \%keys2exit;
}

sub sig_bail { exit $Flag_Signal_Status }
