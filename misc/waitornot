#!/usr/bin/env expect
#
# Waits for the given command to complete, or on receipt of the "any"
# key closes the given command and exits. The complications come mostly
# from exactly how the command and its arguments are run: with a pty or
# not? and what happens to stderr?

package require Tcl 8.5

if {[llength $argv] == 0} {
    puts stderr {Usage: waitornot command [cmd args ..]}
    exit 1
}
# abort with error for edge case of "waitornot sleep 3 <&-"
if {[tell stdin] != -1} {
    puts stderr "waitornot: standard input is closed??"
    exit 1
}

proc bail_out {{code 0}} {
    stty -raw echo
    exit $code
}

proc handle_user_input {fd child_fd} {
    # consume user input so it does not show up on shell afterwards
    read $fd

    # more complicated would be to kill the child pid, which may be
    # necessary if the spawned process misbehaves on close of its input
    # (obtain the PID via [pid $child_fd] or [exp_pid] if spawn is
    # being used)
    close $child_fd

    bail_out
}

proc handle_spawn_input {fd} {
    # pass through output from the program, presumably for consumption
    # by other things
    puts -nonewline [read $fd]

    if {[eof $fd]} { bail_out }
}

# could use "spawn" as provided by expect, though that *merges* stdout and
# stderr from the resultant pty;
#if {[catch {spawn -noecho {*}$argv} msg options]} {
# with open "| ..." the stderr magically vanishes though if necessary
# could be redirected via something like
#if {[catch {set spawn_id [open "| $argv 2>error.out" r]} msg options]} {
if {[catch {set spawn_id [open "| $argv" r]} msg options]} {
    puts stderr "waitornot: could not exec '$argv': $msg"
    bail_out 1
}

# raw mode, no echo, no blocking to best capture the "any" key
stty raw -echo
chan configure stdin -blocking 0
chan event stdin readable [list handle_user_input stdin $spawn_id]

# may also need to set -translation or -encoding, depending on how this
# input needs be handled (or not)
chan configure $spawn_id -blocking 0
chan event $spawn_id readable [list handle_spawn_input $spawn_id]

vwait godot
