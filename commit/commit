#!/usr/bin/perl -w
#
# Wrapper to commit changes back to version control programs.
#
# TODO add status/add checking support, to avoid "commit" grrr! "add"
# "commit" annoyance.
#
# The author disclaims all copyrights and releases this script into the
# public domain.

use strict;

use Cwd qw(getcwd);
use File::Basename;
use File::Spec ();

my %defaults = ( class => 'commit' );

my $prefs_file = $ENV{COMMITCONF} || '~/.commit/prefs';
$prefs_file = tilde_expand($prefs_file);
my $prefs = load_prefs($prefs_file);

use Getopt::Std;
my %opts;
getopts 'h?F:m:r:n', \%opts;

$defaults{revision} = $opts{r} if exists $opts{r};
if ( exists $opts{m} ) {
  $defaults{msg} = $opts{m};
} elsif ( exists $opts{F} ) {
  $defaults{msgfile} = $opts{F};
}

# default to "current" directory if no args (to emulate 'cvs ci'
# without args)
@ARGV = qw(.) unless @ARGV;

# where to store filenames for handling
my ( %tocommit, @unknown );

for my $file (@ARGV) {
  my ( $filename, $filepath, undef ) = fileparse($file);

  if ( -f File::Spec->catfile( $filepath, qw{CVS Root} ) ) {

    push @{ $tocommit{cvs} }, $file unless $file eq 'CVS';

  } elsif ( -f File::Spec->catfile( $filepath, qw{.svn entries} ) ) {

    push @{ $tocommit{svn} }, $file unless $file eq '.svn';

  } elsif ( defined findup('.git') ) {

    push @{ $tocommit{git} }, $file unless $file eq '.git';

  } else {
    push @unknown, $file;
  }
}

for my $commit_type ( sort keys %tocommit ) {

  my %tmp = ( %{ $prefs->{commit}->{$commit_type} }, %defaults );

  # delete "whateveropt" where no "whatever" exists
  for my $option ( keys %tmp ) {
    delete $tmp{$option} if $option =~ /(\w+)opt$/ and not exists $tmp{$1};
  }

  my @command = map {
    s/%{(\w+)}/$tmp{$1}||q{}/ge;
    defined and $_ ne q{} ? $_ : ()
  } split /\s+/, $tmp{exec};

  # for "preview" mode, though this will not show required shell quotes
  # around things like a -m commit message
  unshift @command, 'echo' if exists $opts{n};

  unless ( system( @command, @{ $tocommit{$commit_type} } ) == 0 ) {
    my $exit_status = $? >> 8;
    remark(
      'warning',
      'bad exit status',
      { value => $exit_status, type => $commit_type }
    );
  }
}

if (@unknown) {
  local $" = ',';
  remark( 'warning', 'no method to handle', { files => "@unknown" } );
}

sub findup {
  my $filename = shift;
  my $cur_dir  = getcwd();

  my @dirs = File::Spec->splitdir($cur_dir);
  do {
    my $work_dir = File::Spec->catdir(@dirs);
    if ( -e File::Spec->catfile( $work_dir, $filename ) ) {
      return $work_dir;
    }
    pop @dirs;
  } until ( !@dirs );

  return undef;
}

sub remark {
  my $priority   = shift;
  my $message    = shift;
  my $attributes = shift;

  chomp $message;

  my $attr_str;
  if ($attributes) {
    $attr_str = join ', ',
      map { $attributes->{$_} ||= q{}; "$_=$attributes->{$_}" }
      sort keys %$attributes;
  }

  print STDERR "$priority: $message"
    . ( $attr_str ? ": $attr_str" : q{} ) . "\n";
  return 1;
}

# expands ~username syntax to full home directory path
sub tilde_expand {
  for (@_) {
    s{^ ~      # leading tilde
      ([^/]*)  # preserve following data
     }{
      $1 ? (getpwnam $1)[7] : (getpwuid $<)[7] || $ENV{HOME}
        || remark('error',
             'could not lookup user or HOME not set',
             { file => $_ })
        && exit 99;
     }ex;
  }
  return wantarray ? @_ : "@_";
}

sub load_prefs {
  my $file = shift;

  open my $fh, "< $file"
    or remark( 'error', 'could not load preferences', { file => $file } )
    and exit 101;

  my %prefs;
  my %tmp = %defaults;

  while (<$fh>) {
    s/^\s+//;
    next if /^#/;
    chomp;

    if (/^$/) {
      pref_finish( \%tmp, \%prefs ) if exists $tmp{name};
      %tmp = %defaults;
      next;
    }

    s/\s+$//;

    my ( $k, $v ) = split /\s+/, $_, 2;
    unless ( defined $k and $k =~ /^[\w.-]+$/ and defined $v ) {
      remark(
        'warning',
        'skipping invalid data',
        { file => 'prefs', line => $. }
      );
      next;
    }
    $tmp{$k} = $v;
  }
  pref_finish( \%tmp, \%prefs ) if exists $tmp{name};

  return \%prefs;
}

sub pref_finish {
  my $tmp   = shift;
  my $prefs = shift;

  $prefs->{ $tmp->{class} }->{ $tmp->{name} } = {%$tmp};
}
